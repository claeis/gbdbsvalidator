plugins {
    id 'java'
    id 'maven'

}

version '0.9.2'+System.getProperty('release','-SNAPSHOT')
group 'ch.ehi'

configurations {
    deployerJars
    ftpAntTask
    jaxb
}


repositories {
	mavenLocal()
	mavenCentral()
    maven {
        url "http://jars.interlis.ch"
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'US-ASCII'

// to get the latest SNAPSHOT uncomment the following lines
//configurations.all {
    // check for updates every build
    // resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//}

dependencies {

    compile group: 'ch.ehi', name: 'ehibasics', version: '1.4.1'
	compile	group: 'javax.xml.bind',name:'jaxb-api', version:'2.3.1'
	compile 'org.glassfish.jaxb:jaxb-core:2.3.0.1'
	compile 'org.glassfish.jaxb:jaxb-runtime:2.3.9'
	compile 'com.sun.activation:jakarta.activation:1.2.2'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.6.1'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'
    compile group: 'com.h2database', name: 'h2', version: '1.4.200'
    
    testImplementation 'junit:junit:4.12'
    
    deployerJars "org.apache.maven.wagon:wagon-ftp:3.3.3"
    deployerJars "org.apache.maven.wagon:wagon-ssh:3.3.3"
    
    ftpAntTask "org.apache.ant:ant-commons-net:1.10.7"
    
    jaxb (
    	'org.glassfish.jaxb:jaxb-xjc:2.3.9'
    )
}

Properties properties = new Properties()
File propFile=project.rootProject.file('user.properties')
if(propFile.exists()){
	properties.load(propFile.newDataInputStream())
}
def git = System.getProperty('git',properties.get('git','git'))
def repos_pwd = System.getProperty('repos_pwd',properties.get('repos_pwd','repos_pwd'))
def repos_usr = System.getProperty('repos_usr',properties.get('repos_usr','repos_usr'))
def downloads_pwd = System.getProperty('downloads_pwd',properties.get('downloads_pwd','downloads_pwd'))
def downloads_usr = System.getProperty('downloads_usr',properties.get('downloads_usr','downloads_usr'))
def python= System.getProperty('python',properties.get('python','python'))
def rst2html= System.getProperty('rst2html',properties.get('rst2html','rst2html'))

def generatedResources = "$buildDir/generated-resources/main"
def generatedXjcSources = "$buildDir/xjc/java"

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine git, 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

sourceSets {
    main {
    	output.dir(generatedResources, builtBy: 'generateMyResources')
        java {
            srcDirs = ['src/main/java',"$generatedXjcSources"]
        }
        resources {
            srcDirs =[ 'src/main/xsd', 'src/main/resources']
            include 'META-INF/native-image/**'
            include "**/*.properties"
            include "**/*.xsd"
        }
    }
}

task generateMyResources {
	def versionPropsFile = new File(generatedResources,"ch/ehi/gbdbsvalidator/Version.properties")
	def version="$project.version"
	def hash="SNAPSHOT" // getGitHash()
	inputs.property("version","$project.version")
	inputs.property("hash",hash)
	outputs.file versionPropsFile
	doLast {
		def versionProps = new Properties()
		versionProps.setProperty('version', version)
		versionProps.setProperty('versionCommit', hash)
		versionPropsFile.getParentFile().mkdirs();
		versionProps.store(versionPropsFile.newWriter(), null);
	}
}

task jaxb {
    System.setProperty('javax.xml.accessExternalSchema', 'all')
    inputs.files fileTree(dir: 'src/main/xsd', include: ['*.xsd'])
    inputs.files fileTree(dir: 'src/main/xjb', include: ['*.xjb'])
    outputs.dir "$generatedXjcSources"
    
    doLast {
        mkdir "$generatedXjcSources"

        ant.taskdef(
			name: 'xjc',
			classname: 'com.sun.tools.xjc.XJCTask',
			classpath: configurations.jaxb.asPath
        )

        ant.xjc(
			destdir: "$generatedXjcSources",
			language: 'XMLSCHEMA',
			extension: true,
			encoding: 'US-ASCII'
        ){
            schema(dir: "src/main/xsd", includes: "**/*.xsd")
            binding(dir: "src/main/xjb", includes: "*.xjb")
            //arg(value: '-verbose')
        }
    }
}

task usrdoc(type:Exec) {
	group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = 'Builds the user documentation (html)'
	def infile=new File(project.projectDir,'docs/gbdbsvalidator.rst')
	def outfile=new File(project.buildDir,'docs/gbdbsvalidator.html')
	inputs.file infile
	outputs.file outfile
	doFirst{
		new File(project.buildDir,'docs').mkdir()
	}
		workingDir = project.buildDir
		executable python
		args = [rst2html, infile, outfile]
}

task bindist(type: Zip){
	baseName = project.name
	destinationDir = file('dist')
	from jar
	into('docs'){
		from files("LICENSE","README.md","docs/CHANGELOG.txt",new File(project.buildDir,'docs/gbdbsvalidator.html'))
	}
	into('libs'){
		from configurations.runtimeClasspath
		//def jars=[]
		//subprojects.each {
        //	jars+=it.libsDir
        //}
        //from jars
	}
	// version = '1.0.6'
}

jar {
  manifest {
    attributes(
      "Main-Class": "ch.ehi.gbdbsvalidator.Main",
      "Class-Path": configurations.runtimeClasspath.collect { 'libs/'+it.getName() }.join(' '))
  }
}

artifacts {
	archives(bindist.archivePath) {
        type 'zip'
        classifier 'bindist'
        builtBy bindist
    }
    archives(jar.archivePath){
    	builtBy jar
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
        	configuration = configurations.deployerJars
			repository(url: 'ftp://ftp.interlis.ch'){
            	authentication(userName: repos_usr, password: repos_pwd)
            }
        }
    }
}

task uploadBin(dependsOn: bindist) {
	doLast {
		ant.taskdef(name: 'ftp',
					classname: 'org.apache.tools.ant.taskdefs.optional.net.FTP',
					classpath: configurations.ftpAntTask.asPath)
		def dist= bindist.archiveFile.get().asFile.parent
		def name=bindist.archiveFile.get().asFile.name
		def json = groovy.json.JsonOutput.toJson([filename: 'https://downloads.interlis.ch/gbdbsvalidator/'+name, version: project.version ,date: new Date().format( 'yyyy-MM-dd' )])
		def releaseFile = new File(dist,project.name+"-release.json")
		releaseFile.write(json)
		ant.ftp(server: "jql.ftp.infomaniak.com", userid: downloads_usr, password: downloads_pwd, 
			remoteDir: "/gbdbsvalidator", passive:"yes") {
				fileset(dir: dist ) {
					include(name: name)
					include(name: releaseFile.name)
				}
		}
	}
}